;; Downloaded three LASCO C2 Level 1 images from http://sharpp.nrl.navy.mil/cgi-bin/swdbi/lasco/images/form
;; Download AIA synoptic image from JSOC

;; Read in images
d1 = readfits('25382093.fts.gz', h1)
d2 = readfits('25382094.fts.gz', h2)
d3 = readfits('25382095.fts.gz', h3)

;; Plot difference images
!p.multi=[0,3,1]
plot_image, (d2-d1)>(-5e-10)<(5e-10)
plot_image, (d3-d2)>(-5e-10)<(5e-10)
plot_image, (d3-d1)>(-5e-10)<(5e-10)
!p.multi=0

;; Make solar mask
xx = ((findgen(1024)#replicate(1, 1024))-511.5)*11.9
yy = ((replicate(1, 1024)#findgen(1024))-507.5)*11.9
rr = sqrt(xx^2+yy^2)

!p.multi=[0,3,1]

ii = where(rr le 1900)
sol_mask = fltarr(1024, 1024)+1
sol_mask[ii] = 0
plot_image, sol_mask*(d2-d1)>(-5e-10)<(5e-10)

ii = where(rr le 2000)
sol_mask = fltarr(1024, 1024)+1
sol_mask[ii] = 0
plot_image, sol_mask*(d3-d2)>(-5e-10)<(5e-10)

ii = where(rr le 2100)
sol_mask = fltarr(1024, 1024)+1
sol_mask[ii] = 0
plot_image, sol_mask*(d3-d1)>(-5e-10)<(5e-10)

!p.multi=0

;; Make LASCO image
ii = where(rr le 2000)
sol_mask = fltarr(1024, 1024)+1
sol_mask[ii] = 0
plot_image, sol_mask*(d3-d2)>(-4e-10)<(4e-10)

lasco_image = bytscl(sol_mask*(d3-d2)>(-3e-10)<(3e-10))
lasco_image[where(sol_mask eq 0)] = 0


;; Make AIA image
read_sdo, 'AIA20110804_0348_0131.fits', h, img
aia_image = bytscl(alog10(img)>0.5<2.75)

;; Combine images
window, xs=1024.*11.9/2.4, ys=1024.*11.9/2.4, /pixmap

lasco_image_resize = congrid(lasco_image, 1024.*11.9/2.4, 1024.*11.9/2.4)
tv, lasco_image_resize, 0, 0

aia_lct, rr, gg, bb, wave='131', /load 
tv, aia_image, 511.5*11.9/2.4-512., 507.5*11.9/2.4-512.
loadct, 0

;; Capture and save image
composite_img = tvrd(1500, 1000, 1024.*11.9/2.4-2000, 1024.*11.9/2.4-1600, /true)
write_png, 'lasco_aia_composite_image.png', composite_img

;; Delete all windows
wdelete_all

END
